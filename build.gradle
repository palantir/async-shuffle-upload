/*
 * (c) Copyright 2019 Palantir Technologies Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        gradlePluginPortal()
        maven { url "http://palantir.bintray.com/releases" }    
    }

    dependencies {
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:1.12.4'
        classpath 'com.palantir.baseline:gradle-baseline-java:2.34.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:0.12.2'
        classpath 'gradle.plugin.org.inferred:gradle-processors:3.1.0'
        classpath 'com.palantir.sls-packaging:gradle-sls-packaging:4.3.3'
    }
}

apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.git-version'
apply plugin: 'com.palantir.consistent-versions'

allprojects {
    version gitVersion()
    group 'com.palantir.spark.shuffle.async'

    repositories {
        maven { url "http://palantir.bintray.com/releases" }    
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.inferred.processors'

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Werror']
    }

    tasks.check.dependsOn(javadoc)
    sourceCompatibility = 1.8
    dependencies {
        rootConfiguration platform("org.apache.spark:spark-dist_2.11-hadoop-palantir-bom")
        constraints {
            rootConfiguration 'javax.servlet.jsp:jsp-api:2.2' because 'old jsp-api is incompatible with jersey'
        }
    }

    versionRecommendations {
        excludeConfigurations 'zinc'
    }
}
